buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'  
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'jacoco'
}

version = parent.version

sourceCompatibility = 1.8

configurations {
    externalLibs {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    testImplementation.extendsFrom(externalLibs)
}


dependencies {

    implementation project(':jobs-common')
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.26'
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.26'
    implementation (group: 'gov.ca.cwds.api', name: 'perry', version: perryVersion) {
        exclude group: 'com.zaxxer'
    }

    implementation 'io.dropwizard:dropwizard-hibernate:1.1.0'
    implementation 'com.google.inject:guice:4.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.8.0'
    implementation group: 'gov.ca.cwds.api', name: 'api-core-common', version: coreApiVersion
    implementation group: 'gov.ca.cwds.api', name: 'legacy-data-access', version: coreApiVersion

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testImplementation project(':jobs-common').sourceSets.test.output
    testImplementation(group: 'gov.ca.cwds.api', name: 'api-core-test-support', version: testSupportVersion) {
        exclude group: 'gov.ca.cwds.api'
    }

}

jar {
    manifest {
        attributes 'Main-Class': 'gov.ca.cwds.jobs.audit.AuditEventsJobRunner'
    }
}

shadowJar {
    zip64 true
    classifier = ''
    baseName = 'audit-events-job'
    mergeServiceFiles()
    version version + ((System.properties.getProperty('build') != null) ? '.' + System.properties.getProperty('build') : '')
    dependencies { exclude(dependency('org.slf4j:log4j-over-slf4j:1.7.24'))}
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            groupId 'gov.ca.cwds.jobs'
            artifactId 'jobs-audit-events'
        }
    }
    repositories {
        maven {
            url artifactoryRepo
        }
    }
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled false
	}
}