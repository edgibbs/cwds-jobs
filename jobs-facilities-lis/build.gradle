buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'  
}

version = parent.version

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

configurations {
    externalLibs {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    testImplementation.extendsFrom(externalLibs)
}

dependencies {

    implementation project(':jobs-facilities-common')
    implementation group: 'org.hibernate', name: 'hibernate-c3p0', version: hibernateVersion

    implementation project(':jobs-common')
    implementation 'com.google.inject:guice:4.0'
    implementation 'io.dropwizard:dropwizard-hibernate:1.1.0'  
    implementation group: 'gov.ca.cwds.api', name: 'legacy-data-access', version: coreApiVersion  
    implementation 'gov.ca.cwds:cals-ns-data-model:3.9.10'  
    implementation 'gov.ca.cwds.cals:cals-api:1.11.2'  
    implementation 'org.glassfish.jersey.ext:jersey-declarative-linking:2.23.2'       

    testImplementation project(':jobs-facilities-common').sourceSets.test.output
    testImplementation(group: 'gov.ca.cwds.api', name: 'api-core-test-support', version: testSupportVersion) {
        exclude group: 'gov.ca.cwds.api'
    }

    testImplementation(group: 'gov.ca.cwds', name: 'lis-data-model', version: cwdsModelVersion, changing: true)
    testImplementation(group: 'gov.ca.cwds', name: 'fas-data-model', version: cwdsModelVersion, changing: true)
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: jsonAssertVersion

    testImplementation project(':jobs-common').sourceSets.test.output

}

jar {
    manifest {
        attributes 'Main-Class': 'gov.ca.cwds.jobs.cals.facility.lisfas.LisFacilityJobRunner'
    }
}

shadowJar {
    zip64 true
    classifier = ''
    baseName = 'lis-facilities-job'
    mergeServiceFiles()
    version version + ((System.properties.getProperty('build') != null) ? '.' + System.properties.getProperty('build') : '')
    dependencies { exclude(dependency('org.slf4j:log4j-over-slf4j:1.7.24')) }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            groupId 'gov.ca.cwds.jobs'
            artifactId 'jobs-facilities-lis'
        }
    }
    repositories {
        maven {
            url artifactoryRepo
        }
    }
}
